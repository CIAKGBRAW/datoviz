#version 450

#define WORKGROUP_SIZE 32
#define G .00001
#define m 10
#define M 10000

layout (local_size_x=WORKGROUP_SIZE, local_size_y=1, local_size_z=1) in;

struct Vertex{
    vec3 pos;
    vec3 vel;
    vec4 color;
};

layout(push_constant) uniform Push {
    int n_vertices;
} push;

layout(binding = 0) buffer buf {
   Vertex vertex[];
};

layout (std140, binding = 1) uniform UBO {
    vec4 pos;
    float dt;
} ubo;

void main() {
    if(gl_GlobalInvocationID.x >= push.n_vertices)
        return;
    Vertex v = vertex[gl_GlobalInvocationID.x];

    float s = 0;
    vec3 d = vec3(0);
    for (int i = 0; i < push.n_vertices; i++)
    {
        d = vertex[i].pos - v.pos;
        s = i != gl_GlobalInvocationID.x ? 1.0 / dot(d, d) : 0;
        v.vel += ubo.dt * m * G * d * s;
    }

    // Mouse attractor.
    d = ubo.pos.xyz - v.pos;
    s = 1.0 / dot(d, d);
    v.vel += ubo.dt * M * G * d * s;

    v.pos += ubo.dt * v.vel;

    vertex[gl_GlobalInvocationID.x].pos = v.pos;
    vertex[gl_GlobalInvocationID.x].vel = v.vel;
}
